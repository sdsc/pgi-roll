#%Module1.0
module-whatis "PGI compilers"
module-whatis "Version: VERSION"
module-whatis "Description: PGI compilers (C/C++/Fortran)"

# If unloading, also unload any pgi-dependent modules
if {[module-info mode switch1] || [module-info mode switch3]} {
  # Get the command line that invoked this process; unload only if switching
  # from pgi to another (compiler) module, e.g., "module switch pgi gnu"
  set commandWords [split [exec /bin/ps -p [pid] -o args=] " "]
  set switchFrom [lindex [split [module-info name] "/"] 0]
  set switchTo [lindex [split [lindex $commandWords end] "/"] 0]
  set unloadApps [expr ! [string equal $switchTo $switchFrom]]
} elseif {[module-info mode remove]} {
  set unloadApps 1
} else {
  set unloadApps 0
}
if {$unloadApps && [info exists env(LOADEDMODULES)]} {
  foreach app [lreverse [split $env(LOADEDMODULES) ":"]] {
    if {[string first "pgi/" $app] == 0} {
      continue
    }
    # Check whether this module references a compiler-specific subdir
    set moduleEffects [exec bash -c "module display $app" 2>@1]
    if {[string first "/pgi" $moduleEffects] >= 0} {
      puts stderr "Unloading compiler-dependent module $app"
      module unload [lindex [split $app "/"] 0]
    }
  }
}

# Add all pgi-dependent module dirs to the search path
foreach path [glob -nocomplain /opt/modulefiles/*/.pgi] {
  prepend-path MODULEPATH $path
}

# Compiler environment modifications
setenv PGIHOME /opt/pgi/linux86-64/VERSION
prepend-path PATH "/opt/pgi/linux86-64/VERSION/bin"
prepend-path LD_LIBRARY_PATH "/opt/pgi/linux86-64/VERSION/lib"
prepend-path MANPATH "/opt/pgi/linux86-64/VERSION/man"
